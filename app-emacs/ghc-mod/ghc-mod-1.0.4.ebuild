# Copyright 1999-2011 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header:  $

# ebuild generated by hackport 0.2.14

EAPI="3"

CABAL_FEATURES="bin"
inherit base elisp-common haskell-cabal

DESCRIPTION="Happy Haskell programming on Emacs"
HOMEPAGE="http://www.mew.org/~kazu/proj/ghc-mod/"
SRC_URI="http://hackage.haskell.org/packages/archive/${PN}/${PV}/${P}.tar.gz"

LICENSE="BSD"
SLOT="0"
KEYWORDS="~amd64 ~x86"
IUSE=""

RDEPEND="virtual/emacs"
DEPEND="${RDEPEND}
		>=dev-haskell/attoparsec-0.10
		dev-haskell/attoparsec-enumerator
		>=dev-haskell/cabal-1.6
		dev-haskell/enumerator
		dev-haskell/ghc-paths
		>=dev-haskell/hlint-1.7.1
		dev-haskell/regex-posix
		dev-haskell/transformers
		>=dev-lang/ghc-6.10.1"

SITEFILE=50${PN}-gentoo.el

PATCHES=("${FILESDIR}/${PN}-1.0.4-gentoo.patch")

src_compile() {
	haskell-cabal_src_compile
	pushd elisp
	elisp-compile *.el || die
	popd
}

src_install() {
	haskell-cabal_src_install
	pushd "${S}"
	elisp-install ghc-mod elisp/*.{el,elc}
	elisp-site-file-install "${FILESDIR}"/${SITEFILE}
	popd
}

pkg_postinst() {
	elisp-site-regen
	elog "To configure ghc-mod either add this one line to ~/.emacs:"
	elog "(add-hook 'haskell-mode-hook (lambda () (ghc-init)))"
	elog "or if you wish to use flymake, instead add this one line to ~/.emacs:"
	elog "(add-hook 'haskell-mode-hook (lambda () (ghc-init) (flymake-mode)))"
}

pkg_postrm() {
	elisp-site-regen
}
