# Copyright 1999-2014 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header: $

EAPI=5

# ebuild generated by hackport 0.3.6

CABAL_FEATURES="bin lib profile haddock hoogle hscolour"
inherit haskell-cabal

DESCRIPTION="Shell script analysis tool"
HOMEPAGE="http://www.shellcheck.net/"
SRC_URI="https://github.com/koalaman/${PN}/archive/v${PV}.tar.gz
	-> ${P}.tar.gz"

LICENSE="AGPL-3"
SLOT="0/${PV}"
KEYWORDS="~amd64 ~x86"
IUSE="test"

RDEPEND="dev-haskell/json:=[profile?]
	dev-haskell/mtl:=[profile?]
	dev-haskell/parsec:=[profile?]
	dev-haskell/regex-compat:=[profile?]
	>=dev-lang/ghc-6.10.4:=
"

# The test dependencies are sneaky, quickcheck is required because of a
# ":m +Test.QuickCheck" in a created-on-the-fly ghci session.
DEPEND="${RDEPEND}
	>=dev-haskell/cabal-1.6.0.3
	test? ( dev-haskell/quickcheck:2 )
"

src_prepare() {
	# Shellcheck provides a cabal file, but the author doesn't use it
	# for building at the moment. It almost works, though, so this is
	# the simplest modification that allows the rest of our implicit
	# magic to work.
	echo 'import Distribution.Simple; main = defaultMain;' > Setup.hs \
		|| die 'failed to create Setup.hs'
}

src_test() {
	# You are likely to encounter a minotaur in the bundled test suite.
	einfo ">>> Test phase [custom]: ${CATEGORY}/${PF}"
	./test/runQuack || die 'test suite failed'
}
