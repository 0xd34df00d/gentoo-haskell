# Copyright 1999-2012 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header: $

# ebuild generated by hackport 0.2.17.9999

EAPI=4

CABAL_FEATURES="bin lib profile haddock hoogle hscolour"
inherit haskell-cabal pax-utils

DESCRIPTION="Safely evaluate Haskell expressions"
HOMEPAGE="http://code.haskell.org/mubot/"
SRC_URI="http://hackage.haskell.org/packages/archive/${PN}/${PV}/${P}.tar.gz"

LICENSE="BSD"
SLOT="0"
KEYWORDS="~amd64 ~x86"
IUSE=""

RDEPEND="dev-haskell/cabal[profile?]
		>=dev-haskell/hint-0.3.1[profile?]
		<dev-haskell/mtl-2.2[profile?]
		>=dev-haskell/show-0.3[profile?]
		dev-haskell/utf8-string[profile?]
		>=dev-lang/ghc-6.10.1"
DEPEND="${RDEPEND}
		>=dev-haskell/cabal-1.6"

# default mueval timeout. in 0.7-second units
# Handy to change as lambdabot does not
# allow this bit to confgure
: ${MUEVAL_TIMEOUT:=5}

src_prepare() {
	# Compile with ghc 6.12.3 or ghc 7.0.1
	epatch "${FILESDIR}/${P}-ghc-6.12.3-ghc-7.0.1.patch"
	if has_version ">=dev-lang/ghc-7.0.1"; then
		sed -e 's@ghc-options:   -Wall -static -threaded -O2@ghc-options:   -Wall -static -threaded -O2 -rtsopts@g' \
			-i "${S}/${PN}.cabal" || die "Could not add -rtsopts to ${S}/${PN}.cabal"
	fi

	einfo "Using default mueval timeout: ${MUEVAL_TIMEOUT} * 0.7s"
	sed -e "s@timeLimit = 5@timeLimit = ${MUEVAL_TIMEOUT}@" \
		-i "${S}"/Mueval/ArgsParse.hs || die
}

src_install() {
	cabal_src_install
	# uses GHC-api which needs mmap('rwx') (bug #299709)
	pax-mark -m "${ED}/usr/bin/mueval-core"
}
