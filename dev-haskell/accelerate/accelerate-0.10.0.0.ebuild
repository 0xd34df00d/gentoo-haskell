# Copyright 1999-2012 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header: $

# ebuild generated by hackport 0.2.15

EAPI=4

CABAL_FEATURES="lib profile haddock hoogle hscolour"
inherit haskell-cabal

DESCRIPTION="An embedded language for accelerated array processing"
HOMEPAGE="http://www.cse.unsw.edu.au/~chak/project/accelerate/"
SRC_URI="http://hackage.haskell.org/packages/archive/${PN}/${PV}/${P}.tar.gz"

LICENSE="BSD"
SLOT="0"
KEYWORDS="~amd64 ~x86"
# accelerated-0.10.0.0 cabal file has more use flags, some notes on those:
# llvm: did not try it as stuff is commented out in the cabal file.
# more-pp: broken as wants old versions of blaze-html and text
# pcache: does not compile
IUSE="cuda io"

RDEPEND=">=dev-haskell/mtl-2.0[profile?]
		<dev-haskell/mtl-2.2[profile?]
		cuda? (
			=dev-haskell/binary-0.5*[profile?]
			>=dev-haskell/cuda-0.2.2[profile?]
			>=dev-haskell/fclabels-1.0[profile?]
			<dev-haskell/fclabels-1.2[profile?]
			>=dev-haskell/language-c-0.3[profile?]
			<dev-haskell/language-c-0.5[profile?]
			>=dev-haskell/transformers-0.2[profile?]
			<dev-haskell/transformers-0.4[profile?]
			>=dev-haskell/zlib-0.5[profile?]
			<dev-haskell/zlib-0.6[profile?]
			>=dev-util/nvidia-cuda-toolkit-0.3
		)
		>=dev-lang/ghc-6.12.1"
DEPEND="${RDEPEND}
		>=dev-haskell/cabal-1.6"

src_prepare() {
	sed -e 's@mtl             == 2.0.\*@mtl             >= 2.0 \&\& < 2.2@' \
		-e 's@transformers    == 0.2.\*@transformers    >= 0.2 \&\& < 0.4@' \
		-e 's@zlib            == 0.5.\* && < 0.5.3.2@zlib            >= 0.5 \&\& < 0.6@' \
		-i "${S}/${PN}.cabal" || die "Could not loosen dependencies"
}

src_configure() {
	cabal_src_configure \
		$(cabal_flag cuda) \
		$(cabal_flag io)
}
